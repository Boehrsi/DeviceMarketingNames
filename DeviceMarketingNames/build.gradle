plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'com.dicedmelon.gradle.jacoco-android'
}

group = 'com.github.Boehrsi'
version = '0.3.2'

android {
    namespace = 'de.boehrsi.devicemarketingnames'

    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = group
                artifactId = 'devicemarketingnames'
                version = version

                afterEvaluate {
                    from components.release
                }

                pom {
                    name = 'Device Marketing Names'
                    description = 'Marketing device name lookup library for Android.'
                    url = 'https://github.com/Boehrsi/DeviceMarketingNames'

                    licenses {
                        license {
                            name = 'Apache License 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'Boehrsi'
                            name = 'Boehrsi'
                            email = 'boehrsii@gmail.com'
                        }
                    }
                    scm {
                        url = 'https://github.com/Boehrsi/DeviceMarketingNames'
                        connection = 'scm:git:git://github.com/Boehrsi/DeviceMarketingNames.git'
                        developerConnection = 'scm:git:ssh://git@github.com/Boehrsi/DeviceMarketingNames.git'
                    }
                }
            }
        }
    }
}