plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id "com.vanniktech.maven.publish" version "0.36.0"
    id 'jacoco'
    id "de.undercouch.download" version "5.7.0"
}

ext {
    PUBLISH_GROUP_ID = 'de.boehrsi'
    PUBLISH_VERSION = '1.0.5'
    PUBLISH_ARTIFACT_ID = 'devicemarketingnames'
}

apply from: "${rootProject.projectDir}/scripts/jacoco.gradle"

android {
    namespace = 'de.boehrsi.devicemarketingnames'

    compileSdkVersion 36

    defaultConfig {
        minSdk 21
        targetSdk 36

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            enableUnitTestCoverage = true
            enableAndroidTestCoverage = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }
}

kotlin {
    jvmToolchain(21)
}

dependencies {
    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.13.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
}

mavenPublishing {
    publishToMavenCentral()

    signAllPublications()

    coordinates(PUBLISH_GROUP_ID, PUBLISH_ARTIFACT_ID, PUBLISH_VERSION)

    pom {
        name = PUBLISH_ARTIFACT_ID
        description = 'Marketing device name lookup library for Android.'
        url = 'https://github.com/Boehrsi/DeviceMarketingNames'
        licenses {
            license {
                name = 'Apache License 2.0'
                url = 'https://github.com/Boehrsi/DeviceMarketingNames/blob/main/LICENSE.txt'
            }
        }
        developers {
            developer {
                id = 'Boehrsi'
                name = 'Boehrsi'
                email = 'devicemarketingnames@boehrsi.de'
            }
        }
        scm {
            connection = 'scm:git:github.com/Boehrsi/DeviceMarketingNames.git'
            developerConnection = 'scm:git:ssh://github.com/Boehrsi/DeviceMarketingNames.git'
            url = 'https://github.com/Boehrsi/DeviceMarketingNames/tree/main'
        }
    }
}

tasks.register('downloadData', Download) {
    src 'https://raw.githubusercontent.com/Boehrsi/device_identifiers/refs/heads/main/output/DeviceIdentifiers.kt'
    dest layout.projectDirectory.dir("/src/main/java/de/boehrsi/devicemarketingnames/data")
}